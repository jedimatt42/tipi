; DSR Notes:
; --
; Available WP Registers: R0-R10
;  R11 == Return address
;  R12 == CRUBASE of current device
; Available Scratchpad: >834A - >836D (inclusive) (except?? >8354 and >8356 are preset to locate PAB in VDP)

; This will be all about interacting with PABs in VDP and IO to the Raspberry PI

; So, PAB: Appears to be handed off in a fixed address in scratchpad. The DSR must work backwards from the end.
PABNAM	EQU	>8356		; Address in VDP of Device Name
PABNLE	EQU	>8354		; Length of name (?only trust the LSB?)
; PAB begins at VDP address pointed to by: @>8356 - @>8354 - >0A

; There is room in PAD for the 10 byte header at FAC (floating point accumulator)
;   These equates are relative to WP at DSR entry. (We store that in R10)
PAD	EQU	->E0		; Beginning of scratchpad ram
FAC	EQU	PAD+>4A		; 36 bytes available to DSR We'll copy the PAB header here.
OPCODE	EQU	FAC+>0		; PAB - location of opcode after copied
FLGSTS	EQU	FAC+>1		; PAB - [blah][blah]
BUFADR	EQU	FAC+>2		; PAB - address of read or write buffer
RECLEN	EQU	FAC+>4		; PAB - logical record length
CHRCNT	EQU	FAC+>5		; PAB - character count
RECNUM	EQU	FAC+>6		; PAB - record number
SCNOFF	EQU	FAC+>8		; PAB - screen offset
STATB 	EQU	SCNOFF		; Alias for when SCNOFF makes no sense.
OPTLEN	EQU	FAC+>9		; PAB - option length

; Error codes - RPi will return one of these or SUCCESS after evaluating PAB and device name.
EDVNAME	EQU	>00		; Bad device name
EWPROT	EQU	>01		; Device write protected
EOPATTR	EQU	>02		; Bad open attribute
EILLOP	EQU	>03		; Illegal Operation
ENOSPAC	EQU	>04		; Out of table/buffer space
EEOF	EQU	>05		; Attempt to read past eof
EDEVERR	EQU	>06		; Any device failure
EFILERR	EQU	>07		; Any file failure

; Success code
SUCCESS	EQU	>FF		; Returned from RPi if PAB will be handled.

; Opcodes
OPOPEN	EQU	>0000		; Open file
OPCLOSE	EQU	>0100		; Close file
OPREAD	EQU	>0200		; Read file
OPWRITE	EQU	>0300		; Write file
OPREST	EQU	>0400		; Restore/rewind record
OPLOAD	EQU	>0500		; Load program image file
OPSAVE	EQU	>0600		; Save program image file
OPDEL	EQU	>0700		; Delete file
OPSCRAT	EQU	>0800		; Scratch Record
OPSTAT	EQU	>0900		; Status (varies if file is open or not, RPi should keep state)


; Macros
; Function calling support, such as a alt return address
RET	EQU	>835C		; stash place for previous R11:  MOV R11,@RET   BL sendmsg  MOV @RET,R11
VPAB	EQU	>835A		; once we compute the beginning of PAB in VDP we'll reuse this value.

; Pattern of Register Usage - 
;  R15 - VDPWA
;  R10 - Base address of Workspace, many EQUs are relative to R10

; TIPI Cleanup - used before exit
cleanup
	CLR	R0		; clean up output latches before we go.
	MOVB	R0,@TDOUT
	MOVB	R0,@TCOUT
	RT

; Handle requests to TIPI device
tipidsr
        LIMI	0		; I/O plays with VDP registers and relies on them 
	MOV	R11,@RET	; Stash the dsrlnk Return address
	STWP	R10		; PAB equates are relative to R10, don't mess with R10

	LI	R4,PABNAM	; Begin computing beginning of PAB
	CLR	R9
	A	*R4,R9
	S	@PABNLE,R9	;   rewind to beginning of device name
	LI	R2,>0A
	S	R2,R9		;   rewind over PAB header
; NOW R9 == V@PAB - beginning of VDP PAB, we will preserve that.
	MOV	R9,@VPAB


; Set VDP to read from beginning of PAB that is now in R0 and copy to scratchpad
	.setvdpra R9
	LI	R1,>0A		; while r1 ne 0; do
	MOV	R10,R2		;   R2 will be index relative to scratchpad
!	MOVB	@VDPRD,@OPCODE(R2)	;   load from PAB in vdp
	INC	R2		;   inc index... 
	DEC	R1		;   r1 is our loop counter
	JNE	-!		;   copy more bytes if R1 is not 0

; Send request to RPi
	LI	R0,>0A		; PAB is 10 bytes
	MOV	R10,R1
	AI	R1,OPCODE
	BL	@sendmsg	; send the 10 byte PAB from scratchpad

	MOV	@PABNAM,R1	; Begin computing beginning of PAB device name
	S	@PABNLE,R1	;   rewind to beginning of device name (might need to only consume the LSB of PABNLE
	DEC	R1		;   and back one more to get full device name length
	.setvdpra R1
	MOVB	@VDPRD,R0	; Initialize character counter
	SRL	R0,8		;   just a byte
	INC	R1		; Set address of beginning device name data.
	BL	@vsendmsg	; send the full devicename
        
; Handle result
	LI	R0,1		; Get initial response from RPi (code will be in R9)
	MOV	R10,R1
	CLR	R9		; setup R9 LSB as our receive buffer ( yep 1 byte )
	AI	R1,R9LB		; load single byte message into R9, the response code
	BL	@recvmsg

	CI	R9,EDVNAME	; If device name error ( such as we don't handle it )
	JEQ	tipipass
	CI	R9,SUCCESS	; If not success, we'll pass the code back to the PAB
	JEQ	hresponse

	MOV	R9,R1
	JNE	reterr

; Return without handling device
tipipass
	BL	@cleanup
	MOV	@RET,R11
	RT

; Exit and return an error status back to OS, status expected in R1
reterr
	SRC	R1,3		;   move status to top 3 bits.
	SOCB	@FLGSTS(R10),R1	;   merge with file flags so we don't overwrite them
	MOV	@VPAB,R3	;   compose the address of status byte 
	INC	R3		;   second byte in PAB
	.setvdpwa R3
	MOVB	R1,@VDPWD	;   write the status byte
	JMP	retdone

; RPi thinks it can handle this request :) Good news!
hresponse
	CLR	R1		; switch on opcode in R1
	MOVB	@OPCODE(R10),R1

	CI	R1,OPOPEN
	JEQ	hopen

	CI	R1,OPCLOSE
	JEQ	hclose

	CI	R1,OPREAD
	JEQ	hread

	CI	R1,OPWRITE
	JEQ	hwrite

	CI	R1,OPLOAD
	JEQ	hload

	CI	R1,OPSTAT
	JEQ	hstatus

	LI	R1,EDEVERR	;   default: return device error
	JMP	reterr

; Handle OPCODE 0 - OPEN
hopen
	; quick hack, supposed to return record length if it is zero in the pab request...
	LI	R0,1
	MOV	R10,R1
	AI	R1,R9MB		; Set recv buffer to address of R9 to read an int.
	BL	@recvmsg	; R9 MSB now has record length of open file.
	MOV	@VPAB,R3
	AI	R3,RECLEN-OPCODE
	.setvdpwa R3
	MOVB	R9,@VDPWD
	JMP	retdone

; Handle OPCODE 1 - CLOSE
hclose
	JMP	retdone

; Handle OPCODE 2 - READ
hread
	MOV	@RECNUM(R10),R0
	MOV	@BUFADR(R10),R1
	BL	@vrecvmsg	; R0 is now number of bytes read in record
	SWPB	R0
	MOV	@VPAB,R3
	AI	R3,CHRCNT-OPCODE
	.setvdpwa R3
	MOVB	R0,@VDPWD
	MOV 	@RECNUM(R10),R1	; increment the VDP PAB record number
	INC	R1
	MOV	@VPAB,R3
	AI	R3,RECNUM-OPCODE
	.setvdpwa R3
	MOVB	R1,@VDPWD
	JMP	retdone

; Handle OPCODE 3 - WRITE
hwrite
	CLR	R0
	MOVB	@CHRCNT(R10),R0
	SWPB	R0
	MOV	@BUFADR(R10),R1
	BL	@vsendmsg
	JMP	retdone

; Handle response for OPCODE 5 - LOAD
hload
	MOV	@RECNUM(R10),R0	; VPD space left is limited
	MOV	@BUFADR(R10),R1	; Set VDP location to load in to.
	BL	@vrecvmsg 	; Load image data
	JMP	retdone

; Handle OPCODE 9 - STATUS
hstatus
	; TODO - implement an update to the PAB with EOF and other status bits.
	LI	R0,1
	MOV	R10,R1
	AI	R1,R9MB		; Set recv buffer to address of R9 to read an int.
	BL	@recvmsg	; R9 MSB now has status byte
	MOV	@VPAB,R3
	AI	R3,STATB-OPCODE
	.setvdpwa R3
	MOVB	R9,@VDPWD
	JMP	retdone

; 
; Support 3 possible exit modes:
;   tipi skips the device request
;   tipi experiences an error
;   tipi completes the request


; Exit request complete
retdone
	BL	@cleanup
	MOV	@RET,R11
	INCT	R11
	RT

